//Name : Sooraj Tom, 111501036
//Date : 20-09-2017
//Step : 9 Interrupt 6

alias physicalSP S0;
physicalSP = ([PTBR + 2 * (SP / 512)] * 512) + (SP % 512);

alias sysCallNo S1;
sysCallNo = [physicalSP - 1];

if ( sysCallNo == 9) then            //Exec system call
	alias Filename S2;
	alias i S3;
	alias fatentry S4;
	alias requiredp S5;
	alias allocated S6;
	alias pageno S7;
	alias j S8;
	alias sysopft S9;
	
	Filename = [physicalSP - 3];
	i = FAT;

	//Searching if the file with that name exists
	while(i < (FAT + 512)) do
		if(Filename == [i]) then
			break;
		endif;
		i = i + 8;
	endwhile;
	
	if (i >= (FAT + 512)) then     //A file in that name does not exist
		[physicalSP - 2] = -1;
		ireturn;
	endif;
	
	fatentry = i;               		//Now fatentry contains the address of the FAT entry corresponding to the file

	load(1, [fatentry + 2]);            //Loading the basic block onto scratchpad
	
	i = 0;
	requiredp = 0;
	while(i < 256)do
		if([SCRATCHPAD + i] > 0)then
			requiredp = requiredp + 1;
		endif;
		i = i + 1;
	endwhile;
	
	if(requiredp > 3)then
		[physicalSP - 2] = -1;        //If number of pages in the file is greater than 3
		ireturn;
	endif;
	
	i = 0;
	allocated = 0;
	while(i < 3)do
		if([PTBR + (i * 2) + 1]%10 == "1")then        //Counting valid pages
			allocated = allocated + 1;
		endif;
		i = i + 1;
	endwhile;
	
	if( allocated > requiredp)then                   //Some pages needs to be deallocated
		j = 2;                                       //Begin from the third page
		while(allocated > requiredp)do
			if([PTBR + (j * 2) + 1]%10 == "1")then    //If the valid bit is set as 1
				[PTBR + (j * 2) + 1] = "00";
				[MEM_LIST + [PTBR + (j * 2)]] = 0;    //
				[PTBR + (j * 2)] = -1;
				allocated = allocated - 1;
			endif;
			j = j - 1;
		endwhile;
	endif;
	
	if(allocated < requiredp)then                         //Some more pages need to be allocated
		j = 0;
		while(requiredp > allocated)do
			if([PTBR + (j * 2) + 1] == "00")then          //If we see an invalid page entry
				pageno = 29;
				while(pageno < 64)do                      //Searching for free memory pages
					if([MEM_LIST + pageno] == 0)then
						break;
					endif;
					pageno = pageno + 1;
				endwhile;
				if(pageno == 64)then                       //Free memory pages were not found
					[physicalSP - 2] = -1;
					ireturn;
				endif;
				[MEM_LIST + pageno] = 1;                   //Setting the page as allocated
				[PTBR + (j * 2)] = pageno;                 //Setting the page table entry
				[PTBR + (j * 2) + 1] = "01";
				allocated = allocated + 1;
			endif;
			j = j + 1;
		endwhile;
	endif;
	
	i = 0;
	while(i < requiredp)do
		load([PTBR + (i * 2)], [SCRATCHPAD + i]);          //Loading each page from disk to memory
		i = i + 1;
	endwhile;
	
	i = (PTBR - PAGE_TABLE) / 8;    //Obtaining process id of the calling process
	i = READY_LIST + (32 * i);      //Obtaining the PCB of current process
	i = i + 15;                     //Obtaining per-process open file table
	
	j = 0;
	while(j < 16)do                 
		if([i + j] > -1)then
			sysopft = [i + j];
			sysopft = FILE_TABLE + sysopft * 2;
			if([sysopft + 1] == 1) then           //The file is open only by one process
				[sysopft + 1] = 0;
				[sysopft] = -1;
			else
				[sysopft + 1] = [sysopft + 1] - 1;     //Decrementing the open count for the file
			endif;
		endif;
		j = j + 1;
	endwhile;
	
	SP = 3 * 512;
	
	i = [PTBR + 3 * 2];
	[i * 512] = 0;
	
endif;
ireturn;
